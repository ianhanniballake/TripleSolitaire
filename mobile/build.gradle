apply plugin: 'com.android.application'

dependencies {
    compile 'com.google.android.gms:play-services-games:10.0.1'
}

def versionMajor = 2
def versionMinor = 4
def versionPatch = 0
def versionBuild = 0

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 23
        versionCode versionMajor * 10000000 + versionMinor * 1000000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    signingConfigs {
        release {
            storeFile System.getenv("KEYSTORE") != null ? file(System.getenv("KEYSTORE")) : null
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("TRIPLE_SOLITAIRE_ALIAS")
            keyPassword System.getenv("TRIPLE_SOLITAIRE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }
}
