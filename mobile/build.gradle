apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.crashlytics'

dependencies {
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.google.android.gms:play-services-games:21.0.0'
    implementation 'com.google.firebase:firebase-analytics-ktx:18.0.3'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:17.4.1'
}

def versionMajor = 2
def versionMinor = 6
def versionPatch = 0
def versionBuild = 0

android {
    namespace 'com.github.triplesolitaire'
    compileSdk 31

    defaultConfig {
        minSdkVersion 16
        targetSdk 31
        versionCode versionMajor * 10000000 + versionMinor * 1000000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    signingConfigs {
        release {
            storeFile System.getenv("KEYSTORE") != null ? file(System.getenv("KEYSTORE")) : null
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("TRIPLE_SOLITAIRE_ALIAS")
            keyPassword System.getenv("TRIPLE_SOLITAIRE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

apply plugin: 'com.google.gms.google-services'